#!/usr/bin/perl
# vi: set ts=4 sw=4 :

use warnings;
use strict;

=pod

Usage: add [-a] -e [NUM ...]
       add [-a] [FILE ...]

Adds numbers and outputs the total.  If "-a" is specified (and at least one
number was read), also output the mean average, with two decimal places.

In the first form, the numbers to add are given as command line arguments.  In
the second form, the given FILEs (or standard input) are read, and each line
is interpreted as a number.  Non-numeric arguments / lines are ignored.

Example:

  "add -a -e 1 2 4 frog 5" 

produces the output

  12      3.00

=cut

use Math::BigFloat;
my $t = Math::BigFloat->new(0);
my $n = 0;

my $average;
$average = 1, shift if @ARGV and $ARGV[0] eq "-a";

if (@ARGV and $ARGV[0] eq "-e") {
	shift;
	add($_) for @ARGV;
} else {
	while (<>) {
		chomp;
		add($_);
	}
}

print $t;
if ($average and $n) {
	print "\t";
	printf "%.2f", $t/$n;
}
print "\n";

sub add {
	my $arg = Math::BigFloat->new(shift);
	$t += $arg, ++$n unless $arg->is_nan;
}
