#!/usr/bin/perl
# vi: set ts=4 sw=4 :

use warnings;
use strict;

=pod

Usage: realpath PATH
       realpath

Perform realpath(3) translation.  In the first form, translate PATH; in the
second, lines are read from standard input, and the translation performed on
each line.  Results are sent to standard output.

=cut

use Cwd 'realpath';

my $errors;

if (@ARGV) {
	do_it(shift);
} else {
	while (<>) {
		chomp;
		do_it($_);
	}
}

exit($errors ? 1 : 0);

sub do_it {
	my $in = shift;
	if (defined(my $path = realpath($in))) {
		print "$path\n";
	} else {
		print STDERR "realpath '$in': $!\n";
		++$errors;
	}
}

# eof realpath
