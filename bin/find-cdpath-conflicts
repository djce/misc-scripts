#!/usr/bin/env perl
# vi: set ts=4 sw=4 :

use warnings;
use strict;

# Scans directories on your $CDPATH for (immediate child) subdirectories; for
# each subdirectory name found in more than one such directory, shows the
# subdirectory name and the locations in which it was found.

my @dirs = split /:/, $ENV{CDPATH};
my %whereis;

my %done_dir;

for my $dir (@dirs) {
	next if $done_dir{$dir}++;
	-d($dir) or warn("Warning: $dir (on your CDPATH) is not a directory\n"), next;

	use File::Find qw( find );
	find(
		sub {
			return if $_ eq ".";
			# Pretty sure that File::Find promised to stat() for us,
			# but it doesn't always seem to be true...
			stat $File::Find::name or return;
			push @{ $whereis{$_} }, $dir
				if -d(_);
			$File::Find::prune = 1;
		},
		$dir,
	);
}

for my $found (sort keys %whereis) {
	my $dirs = $whereis{$found};
	delete $whereis{$found}, next if @$dirs == 1;
	print "$found:";
	print " $_/$found" for @$dirs;
	print "\n";
}

print "No conflicts found\n" if not %whereis;

# eof find-path-conflicts
